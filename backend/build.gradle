plugins {
    id 'java'
    id 'org.springframework.boot' version '2.0.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'net.ltgt.apt' version '0.10'
    id 'application'
    id 'org.liquibase.gradle' version '2.0.1'
}

sourceCompatibility = 11
targetCompatibility = 11

bootRun {
    systemProperties = [
            'spring.profiles.active': env
    ]
}

def props = new Properties()
layout
        .files("src/main/resources/application.properties", "src/main/resources/application-${env}.properties")
        .collect()
        .each {
            it.withInputStream {
                props.load(it)
            }
        }


props.each {
    ext.set(it.key, it.value)
}

//println ext['spring.liquibase.change-log']
//println ext['spring.datasource.url']

liquibase {
    activities {
        main {
            changeLogFile project.ext['spring.liquibase.change-log']
            url project.ext['spring.datasource.url']
            username project.ext['spring.datasource.username']
            password project.ext['spring.datasource.password']
            databaseChangeLogTableName project.ext['spring.liquibase.database-change-log-table']
            databaseChangeLogLockTableName project.ext['spring.liquibase.database-change-log-lock-table']
        }
    }

    runList = 'main'
}

test {
    useJUnitPlatform()
    systemProperties = [
            'spring.profiles.active': env
    ]
}

mainClassName = 'boot.TaskManagement'

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:2.0.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-web:2.0.5.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-security:2.0.5.RELEASE'

    compile 'org.postgresql:postgresql:42.2.2'
    compile 'javax.xml.bind:jaxb-api:2.2.11'
    compile 'org.javassist:javassist:3.23.1-GA'

    compileOnly 'org.projectlombok:lombok:1.18.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.2'

    testCompile ('org.springframework.boot:spring-boot-starter-test:2.0.5.RELEASE') {
        exclude module: 'junit'
    }

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    liquibaseRuntime 'org.liquibase:liquibase-core:3.6.3'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.0.1'
    liquibaseRuntime 'org.postgresql:postgresql:42.2.2'
}
